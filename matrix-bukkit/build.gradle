plugins {
    id 'com.github.johnrengelman.shadow' version '6.0.0'
}

dependencies {
    compileOnly project(':matrix-api')
    compileOnly project(':matrix-common')
    compileOnly 'redis.clients:jedis:3.3.0'
    compileOnly 'com.github.ben-manes.caffeine:caffeine:2.8.4'
    compileOnly 'org.apache.commons:commons-pool2:2.6.2'
    compileOnly 'org.mongodb:mongo-java-driver:3.12.5'
    compileOnly 'org.mongodb.morphia:morphia:1.3.2'
    compileOnly 'com.zaxxer:HikariCP:3.4.5'
    compileOnly 'com.destroystokyo.paper:paper-api:1.15.2-R0.1-SNAPSHOT'
    compileOnly 'com.destroystokyo.paper:paper:1.15.2-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.10.6'
    compileOnly 'com.vexsoftware:nuvotifier-universal:2.6.0'
}

artifacts {
    archives shadowJar
}

shadowJar {
    getArchiveClassifier().set(null)
    getArchiveVersion().set(null)

    configurations = [project.configurations.compileOnly]
    dependencies {
        include(project(':matrix-api'))
        include(project(':matrix-common'))
    }

    relocate 'com.github.benmanes.caffeine', 'com.github.beelzebu.lib.caffeine'
    relocate 'org.mariadb.jdbc', 'com.github.beelzebu.lib.mariadb'
    relocate 'com.zaxxer.hikari', 'com.github.beelzebu.lib.hikari'
    relocate 'redis.clients.jedis', 'com.github.beelzebu.lib.jedis'
    relocate 'org.apache.commons.pool2', 'com.github.beelzebu.lib.commonspool2'
}

processResources {
    duplicatesStrategy(DuplicatesStrategy.INCLUDE)
    from(sourceSets.main.resources.srcDirs) {
        expand 'pluginVersion': project.version
        include 'plugin.yml'
    }
}

description = 'Matrix Bukkit'
